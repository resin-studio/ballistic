Function:
  params+=ID[','] '=>' (spec=Spec)? (body=Body)?
;

Spec:
  '{' result=ID '|' args+=ID[','] ': data' '}'
;

Body: Sample | Plate | Final ;
Sample: name = ID '~' src = Distribution  ';' contin = Body ;
Plate: name = ID '#' size = INT '~' src = Distribution  ';' contin = Body ;
Final: content = Distribution;

Distribution: Normal | Lognorm | Uniform | Halfnorm | Direct ;
Normal: 'normal' '(' mean=Expr ',' sigma=Expr ')' ;
Lognorm: 'lognorm' '(' mean=Expr ',' sigma=Expr ')' ;
Uniform: 'uniform' '(' low=Expr ',' high=Expr ')' ;
Halfnorm: 'halfnorm' '(' scale=Expr ')' ;
Direct: '@' '(' content=Expr ')'  ;


Expr: base=Prod exts*=Ext;
Ext: Plus | Minus;
Plus: '+' arg=Prod;
Minus: '-' arg=Prod;

Prod: base=Atom factors*=Factor;
Factor: Mul | Div;
Mul: '*' arg=Atom ;
Div: '/' arg=Atom ;

Atom: Paren | Mean | Project | ID | FLOAT ;

Paren:  '(' content=Expr ')' ;
Mean: 'mean' '(' vector=ID ')' ;
Project: vector=ID '[' index = Expr ']' ;